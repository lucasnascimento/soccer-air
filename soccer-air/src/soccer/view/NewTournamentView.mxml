<?xml version="1.0" encoding="utf-8"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 styleName="newTournamentStyle"
		 title="Criar Novo Campeonato"
		 width="400" height="300">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.DragEvent;
			
			import soccer.model.Club;
			import soccer.model.Manager;
			import soccer.utils.VectorUtil;

			private var _clubs:Array;
			public var managers:Vector.<Manager>;
			public var numVisibleItems:uint    = 5;
			public var itemsHeight:Number      = 80;
			public var autoCompleteTime:Number = 100; //em milisegundos

			[Bindable]
			protected var clubsCollection:ArrayCollection;
			[Bindable]
			protected var managersCollection:ArrayCollection;

			private var _autoCompleteID:uint;
			private var _lastSearchedText:String;

			public function setClubs( value:Vector.<Club> ):void {
				_clubs = VectorUtil.toArray( value );
			}

			override protected function createChildren():void {
				super.createChildren();

				managers = new Vector.<Manager>();
				
				managersCollection = new ArrayCollection();

				clubsCollection = new ArrayCollection( _clubs );
				clubsCollection.filterFunction = filterClubs;
				updateListHeight();
			}

			protected function filterClubs( club:Club ):Boolean {
				var clubName:String = club.name.toLocaleUpperCase();
				var search:String   = tiClub.text.toLocaleUpperCase();
				return clubName.indexOf( search ) >= 0;
			}

			protected function tiClub_keyUpHandler( event:KeyboardEvent ):void {
				clearTimeout( _autoCompleteID );
				if( event.keyCode == Keyboard.ENTER ) {
					addCurrentManagerInfo();
				} else if( _lastSearchedText != tiClub.text ) {
					_autoCompleteID = setTimeout( refreshClubsCollection, autoCompleteTime );
				}
			}
			
			protected function tiClub_keyDownHandler( event:KeyboardEvent ):void {
				if( lstClubs.selectedItem != null && ( event.keyCode == Keyboard.UP || event.keyCode == Keyboard.DOWN ) ) {
					var length:uint = clubsCollection.length;

					if( event.keyCode == Keyboard.UP ) {
						if( lstClubs.selectedIndex > 0 ) {
							lstClubs.selectedIndex--;
						} else {
							lstClubs.selectedIndex = length - 1;
						}
					} else if( event.keyCode == Keyboard.DOWN ) {
						if( lstClubs.selectedIndex < length - 1 ) {
							lstClubs.selectedIndex++;
						} else {
							lstClubs.selectedIndex = 0;
						}
					}
					lstClubs.ensureIndexIsVisible( lstClubs.selectedIndex );
				}
			}
			
			protected function refreshClubsCollection():void {
				clubsCollection.refresh();
				if( clubsCollection.length > 0 ) {
					lstClubs.selectedIndex = 0;
				}
				lstClubs.visible  = tiClub.text.length > 0 && clubsCollection.length > 0;
				_lastSearchedText = tiClub.text;
			}

			protected function updateListHeight():void {
				if( clubsCollection.length > numVisibleItems ) {
					lstClubs.height = numVisibleItems * itemsHeight;
				} else {
					lstClubs.height = clubsCollection.length * itemsHeight; 
				}
			}
			
			protected function btnAddManager_clickHandler( event:MouseEvent):void {
				addCurrentManagerInfo( true );
			}

			protected function lstClubs_clickHandler( event:MouseEvent ):void {
				addCurrentManagerInfo();
			}

			protected function managersListLabelFunction( manager:Manager ):String {
				var rank:int = managersCollection.getItemIndex( manager ) + 1;
				return rank + ") " + manager.name + "/" + manager.favoriteClub.name;
			}

			protected function addCurrentManagerInfo( showError:Boolean = false ):void {
				var name:String = tiManager.text == "" ? null : tiManager.text;
				var club:String = tiClub.text    == "" ? null : tiManager.text;
				var manager:Manager = new Manager( name, lstClubs.selectedItem as Club );
				
				if( name != null && club != null ) {
					managersCollection.addItem( manager );

					tiManager.setFocus();
					tiManager.text = tiClub.text = "";
					refreshClubsCollection();
				} else if( name == null ) {
					if( showError ) {
						Alert.show( "Por favor complete o nome do treinador.", "Criação de torneio" );
					}
					tiManager.setFocus();
				} else if( club == null ) {
					if( showError ) {
						Alert.show( "Por favor complete o clube.", "Criação de torneio" );
					}
					tiClub.setFocus();
				}
			}
			
			protected function lstManagers_dragCompleteHandler( event:DragEvent ):void {
				managersCollection.refresh();
			}
			
			protected function lstManagers_doubleClickHandler( event:MouseEvent ):void {
				var manager:Manager = lstManagers.selectedItem as Manager;
				if( manager != null ) {
					tiManager.text = manager.name;
					tiClub.text    = manager.favoriteClub.name;
				}
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<!-- Manter esta ordem para que a lista de auto complete se mantenha sobre a lista de treinadores -->
	<s:Group x="200" y="10">
		<s:layout>
			<s:VerticalLayout />
		</s:layout>
		<s:Label text="Lista de Treinadores" />
		<s:List id="lstManagers"
				labelFunction="managersListLabelFunction"
				width="180" height="200" dataProvider="{managersCollection}"
				doubleClickEnabled="true" doubleClick="lstManagers_doubleClickHandler(event)"
				dropEnabled="true" dragMoveEnabled="true"
				dragEnabled="true" allowMultipleSelection="true" 
				dragComplete="lstManagers_dragCompleteHandler(event)" />
		<s:Button label="Avançar" />
	</s:Group>
	<s:Group x="10" y="10">
		<s:layout>
			<s:VerticalLayout />
		</s:layout>
		<s:Label text="Treinador" />
		<s:TextInput id="tiManager" width="165" />
		<s:Label text="Clube" />
		<s:Group>
			<s:layout>
				<s:HorizontalLayout />
			</s:layout>
			<s:TextInput id="tiClub" width="120" keyUp="tiClub_keyUpHandler(event)" keyDown="tiClub_keyDownHandler(event)" />
			<s:Button id="btnAddManager" width="40" label="Ok" click="btnAddManager_clickHandler(event)" />
		</s:Group>
		<s:List id="lstClubs" visible="false" width="400" dataProvider="{clubsCollection}"
			click="lstClubs_clickHandler(event)"
			itemRenderer="soccer.view.SoccerClubListItemRenderer" />
	</s:Group>
</s:Panel>
